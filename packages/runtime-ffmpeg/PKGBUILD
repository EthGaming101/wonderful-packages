# TODO: trim down decoder/parser/demuxer/bsf list

# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=(runtime-ffmpeg runtime-ffmpeg-dev)
pkgver=5.0.1
pkgrel=1
epoch=
pkgdesc="ffmpeg video library"
arch=("i686" "x86_64")
url="http://www.ffmpeg.org"
license=("MIT")
source=("https://www.ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz")
depends=(runtime-musl)
makedepends=(runtime-musl-dev)
sha256sums=('ef2efae259ce80a240de48ec85ecb062cecca26e4352ffb3fda562c21a93007b')

. "/wf/config/runtime-env-vars.sh"

build() {
	cd "ffmpeg-$pkgver"
	./configure \
		--prefix="$WF_PATH" \
		--extra-cflags="$WF_RUNTIME_INCLUDES" \
		--extra-ldflags="$WF_RUNTIME_LDFLAGS" \
		--disable-static --enable-shared \
		--disable-gpl --disable-version3 \
		--disable-programs --disable-network \
		--disable-autodetect --disable-iconv \
		--disable-doc \
		--disable-avfilter --disable-postproc \
		--disable-everything \
		--enable-decoder=ffv1,ffvhuff,huffyuv,vp8,vp9 \
		--enable-decoder=mjpeg,bmp,png,flac,vorbis,theora \
		--enable-decoder=rawvideo,pcm_f16le,pcm_f32le,pcm_s16le \
		--enable-decoder=pcm_u16le,pcm_mulaw,pcm_alaw,pcm_u8,pcm_s8 \
		--enable-decoder=opus \
		--enable-parser=bmp,flac,mjpeg,opus,png,vorbis,vp8,vp9 \
		--enable-demuxer=apng,avi,flac,matroska,ogg,wav \
		--enable-demuxer=pcm_alaw,pcm_s16le,pcm_u16le,pcm_s8,pcm_u8 \
		--enable-demuxer=image2,mov,rawvideo \
		--enable-protocol=file
	make
}

package_runtime-ffmpeg() {
	cd "ffmpeg-$pkgver"
	make DESTDIR="$pkgdir" install
	cd "$pkgdir"
	wf_relocate_path_to_destdir
	rm -r include share lib/pkgconfig
}

package_runtime-ffmpeg-dev() {
	pkgdesc="ffmpeg runtime development files"
	depends=(runtime-ffmpeg)

	cd "ffmpeg-$pkgver"
	make DESTDIR="$pkgdir" install
	cd "$pkgdir"
	wf_relocate_path_to_destdir
	rm -r lib/*.so lib/*.so.*
}
