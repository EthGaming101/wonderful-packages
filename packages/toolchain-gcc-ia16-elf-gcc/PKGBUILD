# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=(toolchain-gcc-ia16-elf-gcc)
# toolchain-gcc-ia16-elf-gcc-libs)
_pkgver=6.3.0
pkgver=6.3.0.r147112.c763cb6e365
pkgrel=1
_gmpver=6.1.2
_mpfrver=3.1.5
_mpcver=1.0.3
_islver=0.16.1
epoch=
pkgdesc="The GNU Compiler Collection"
arch=("i686" "x86_64")
depends=(runtime-gcc-libs runtime-musl toolchain-gcc-ia16-elf-binutils)
makedepends=(runtime-musl-dev)
groups=(toolchain-gcc-ia16-elf)
url="https://github.com/WonderfulToolchain/gcc-ia16"
license=("GPL-3.0-or-later")
source=("$pkgname::git+https://github.com/WonderfulToolchain/gcc-ia16#branch=wonderful-main"
        "http://gmplib.org/download/gmp/gmp-$_gmpver.tar.xz"
        "http://www.mpfr.org/mpfr-$_mpfrver/mpfr-$_mpfrver.tar.xz"
        "http://ftp.gnu.org/gnu/mpc/mpc-$_mpcver.tar.gz"
	"https://libisl.sourceforge.io/isl-$_islver.tar.xz")
sha256sums=(
	'SKIP'
	'87b565e89a9a684fe4ebeeddb8399dce2599f9c9049854ca8c0dfbdea0e21912'
	'015fde82b3979fbe5f83501986d328331ba8ddf008c1ff3da3c238f49ca062bc'
	'617decc6ea09889fb08ede330917a00b16809b8db88c29c31bfbb49cbf88ecc3'
	'45292f30b3cb8b9c03009804024df72a79e9b5ab89e41c94752d6ea58a1e4b02'
)

. "/wf/config/runtime-env-vars.sh"

prepare() {
	mkdir -p "gcc-build"
	cd "$pkgname"

	# HACK: hijack RTEMS's libstdc++ crossconfig for our own purposes (which has the dynamic feature checks we want)
	sed -i "s/\*-rtems\*/*-unknown*/" libstdc++-v3/configure

	ln -s ../"gmp-$_gmpver" gmp
	ln -s ../"mpfr-$_mpfrver" mpfr
	ln -s ../"mpc-$_mpcver" mpc
	ln -s ../"isl-$_islver" isl
}

pkgver() {
	pushd "$pkgname" >/dev/null
	printf "%s.r%s.%s" "$_pkgver" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	popd >/dev/null
}

build() {
	cd gcc-build
	../"$pkgname"/configure \
		--prefix="/opt/wonderful/toolchain/gcc-ia16-elf" \
		--target=ia16-elf \
		--with-bugurl="http://github.com/WonderfulToolchain/gcc-ia16/issues" \
		--with-stage1-ldflags="$WF_RUNTIME_LDFLAGS" \
		--without-headers \
		--enable-languages=c \
		--disable-bootstrap \
		--disable-gcov \
		--disable-nls \
		--disable-shared \
		--disable-werror \
		--disable-libquadmath \
		--disable-libssp \
		--disable-libstdcxx \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--disable-multilib \
		--with-isl

	make
}

package_toolchain-gcc-ia16-elf-gcc() {
	cd gcc-build
	make DESTDIR="$pkgdir" install-gcc install-libcc1
	cd "$pkgdir"
	wf_relocate_path_to_destdir
	rm toolchain/gcc-ia16-elf/share/info/dir
}

#package_toolchain-gcc-ia16-elf-gcc-libs() {
#	pkgdesc="GCC-provided libraries"
#	depends=("toolchain-gcc-ia16-elf-binutils" "toolchain-gcc-ia16-elf-gcc")
#	options=(!strip)
#
#	cd gcc-build
#	make DESTDIR="$pkgdir" install-target-libgcc
#	cd "$pkgdir"
#	wf_relocate_path_to_destdir
#}
